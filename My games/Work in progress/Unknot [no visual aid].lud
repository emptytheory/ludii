(game "Unknot"
    (players 2)
    (equipment
        { 
            (board (hex <Board>))
            (piece "Disc" P1)
            (piece "thinCross" Neutral)
        }
    )
    (rules
        (meta (swap))
        (play
            (move Add (piece "Disc1")
                (to
                    (difference (sites Empty) (sites {(centrePoint)}))
                    if:(is Empty 
                        (ahead (to) 
                            (if
                                (is Even (mover))
                                SSW
                                SSE
                            )
                        )
                    )
                )
            )
        )
        (end
            (if 
                (and
                    (is Loop (id P1))
                    (<
                        0
                        (count Sites
                            in:(intersection
                               (sites Loop
                                   inside:True
                                   (id P1)
                               )
                               (sites Empty)
                            )
                        )
                    )
                )
                (result Mover Win)
            )
        )
    )
)

(option "Board Size" <Board> args:{ <size> }
    {
    (item "4"    <4>  "A base 4 board is currently selected")*
    (item "5"    <5>  "A base 5 board is currently selected")
    }
)

(metadata
    (graphics {
        (piece Colour "Disc1" fillColour:(colour Red))
        (region Colour (sites Board) (colour 250 210 148))
        (board Colour InnerEdges (colour 178 151 100))    
        (board Colour OuterEdges (colour 178 151 100))    
    })
)