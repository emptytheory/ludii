(define "Slingshot"
    (forEach Group Orthogonal
        if:(= (mover) (who at:(to)))
        (move Add
            (to
                (sites Direction 
                    from:(sites)
                    Orthogonal
                    stop:(is Occupied (to))
                    distance:(count Sites in:(sites))
                )
                if:(>= 
                    (count Sites in:(sites))
                    (count Steps
                        (to)
                        (sites)
                    )
                )
            )
        )
        (then
            (if
                (<
                    0
                    (count Sites in:(sites Around (last To) Enemy Orthogonal))
                )
                (forEach Site
                    (sites Around (last To) Enemy Orthogonal)
                    (remove 
                        (difference
                            (sites Group at:(site) Orthogonal)
                            (sites Around (sites Occupied by:Mover) Enemy Orthogonal)
                        )
                    )
                )
            )
        )
    )
)


(game "NotMeridians"
    (players 2)
    (equipment
        {
            (board (rotate 30 (hex <Size>)))
            (piece "Disc" Each)
        }
    )
    (rules
        (start {
            (place "Disc1" (min (array (sites Board))))
            (place "Disc2" (max (array (sites Board))))

        })
        (play
            ("Slingshot")
        )
        (end 
            (if 
                (is Full)
                (byScore {
                    (score P1 (count Sites in:(sites  Occupied by:P1)))
                    (score P2 (count Sites in:(sites  Occupied by:P2)))
                })
            )
        )
    )
)

(option "Size" <Size> args:{ <size> }
    {
    (item "4"    <4>  "A size 4 board is currently selected")
    (item "5"    <5>  "A size 5 board is currently selected")
    (item "6"    <6>  "A size 6 board is currently selected")*
    (item "7"    <7>  "A size 7 board is currently selected")
    (item "8"    <8>  "A size 8 board is currently selected")
    }
)

(metadata
    (info {
        (description
"Slingshot … ."
        )
        (rules
"Each turn … ."
        )
        (version "1.2.7")
        (credit "Michael Amundsen")
        (author "Alek Erickson and Michael Amundsen.")
        (date "2021")
    })
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (region Colour (sites Board) (colour 250 210 148))
        (board Colour InnerEdges (colour 178 151 100))
        (board Colour OuterEdges (colour 178 151 100))
    })
)

