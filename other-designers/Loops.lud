(define "IsALoop"
	(not
		(all Sites (sites Board)
			if:(> Infinity
				(count Steps
					(step 
						(to 
							if:(not 
								(is In (to) #1)
							)
						)
					)
					(site)
					(difference
						(sites Board)
						(union 
							(sites Group at:(site) if:(not (is In (to) #1)))
							#1
						)
					)
				)
			)
		)
	)
)

(define "UpdateScoreMover"
	(and
		(set Score Mover 0)
		(forEach Group Orthogonal
			if:(is Mover (who at:(to)))
			(if 
				("IsALoop" (sites))
				(addScore Mover (count Sites in:(sites)))
				(addScore Mover (- (count Sites in:(sites))))
			)
		)
	)
)

(game "Loops"
    (players 2)
    (equipment
        {
            (board (hex <Size>))
            (piece "Disc" Each)
        }
    )
    (rules
		(meta (swap))
        (play
            (move Add
                (to (sites Empty))
				<Score>
            )
        )
        (end 
            (if 
                (is Full) 
                (byScore)
            )
        )
    )
)

(option "Running Score" <Score> args:{ <score> }
	{
	(item "On"  <(then ("UpdateScoreMover"))>  "The score is currently updated after each move. (This makes the AI (even) weaker.)")
	(item "Off" <(then (if (or (is Full) (= 1 (count Sites in:(sites Empty)))) ("UpdateScoreMover")))>  "The score is currently updated only when the board is full. (This makes the AI slightly stronger.)")*
	}
)

(option "Board Size" <Size> args:{ <size> }
    {
    (item "4"    <4>  "A size 4 board is currently selected")
    (item "5"    <5>  "A size 5 board is currently selected")
    (item "6"    <6>  "A size 6 board is currently selected")*
    (item "7"    <7>  "A size 7 board is currently selected")
    (item "8"    <8>  "A size 8 board is currently selected")
    (item "9"    <9>  "A size 9 board is currently selected")
    }
)

(metadata
    (info {
        (description
"Loops is a drawless territory game for the hexhex board. It was invented by Brian Wittmann in 2013."
		)
		(rules 
"
Definition: A loop is a group that severs two hexes from each other in the sense that all paths between them go through the group.

Play: Players take turns placing one stone of their color on any empty hex.

Goal: When the board is full, the game is over and the player with the higher score is the winner.

Scoring: Each stone is worth 1 point for its owner if it is part of a loop, and -1 point otherwise.

Note: This way of scoring is equivalent to the one Brian Wittmann describes, and it's implemented here only for technical reasons. Originally, each stone is worth 1 point for its owner if it is part of a loop, and 1 point for the opponent of its owner otherwise."
		)
        (version "1.3.5")
        (credit "Michael Amundsen")
        (author "Brian Wittmann")
        (date "2013")
    })
	(graphics {
		(player Colour P1 (colour Black))
		(player Colour P2 (colour White))
		(region Colour (sites Board) (colour 250 210 148))
		(board Colour InnerEdges (colour 178 151 100))
		(board Colour OuterEdges (colour 178 151 100))
	})
    (ai
		(bestAgent "MC-GRAVE")
    )
)