(define "FillTerritories"
    (forEach Group Orthogonal if:(is Empty (to))
        (if 
            (all Sites
                (sites)
                if:(< 1 (count Sites in:(sites Around (site) NotEmpty Orthogonal)))
            )
            (if 
                (> 
                    (count Sites in:(sites Around (sites) Own Orthogonal))
                    (count Sites in:(sites Around (sites) Enemy Orthogonal))         
                )
                (add (piece (mover)) (to (sites)))
                (add (piece (next)) (to (sites)))
            )
        )
    )
)

(define "IsNotWeaklyConnected"
    (all Sites
        (sites Around (var "FirstPlacement") Own Diagonal)
        if:(< 0 
            (count Sites 
                in:(intersection
                    (sites Around (var "FirstPlacement") Own Orthogonal)
                    (sites Around (site) Own Orthogonal)
                )
            )
        )
    )
)

(game "Quentin"
    (players 2)
    (equipment {
            (board (square <Board:size>) use:Vertex)
            (piece "Marker" Each)
            (regions P1 { (sites Side N) (sites Side S) } )
            (regions P2 { (sites Side W) (sites Side E) } )
    })
    (rules
        (meta (swap))
        (play
            (do
                (move Add
                    (to 
                        (sites Empty)
                        (apply (set Var "FirstPlacement" (to)))
                    )
                    (then ("FillTerritories"))
                )
                ifAfterwards:("IsNotWeaklyConnected")
            )
        )
        (end {
            (if (is Connected Orthogonal Mover) (result Mover Win))
            (if (is Blocked Orthogonal Mover) (result Mover Loss))
        })
    )
)

(option "Board Size" <Board> args:{ <size> }
    {
    (item "4x4"     <4>  "A 4x4 board is currently selected")
    (item "5x5"     <5>  "A 5x5 board is currently selected")
    (item "6x6"     <6>  "A 6x6 board is currently selected")
    (item "7x7"     <7>  "A 7x7 board is currently selected")
    (item "8x8"     <8>  "An 8x8 board is currently selected")
    (item "9x9"     <9>  "A 9x9 board is currently selected")
    (item "10x10"  <10>  "A 10x10 board is currently selected")
    (item "11x11"  <11>  "An 11x11 board is currently selected")
    (item "12x12"  <12>  "A 12x12 board is currently selected")*
    (item "13x13"  <13>  "A 13x13 board is currently selected")
    (item "14x14"  <14>  "A 14x14 board is currently selected")
    (item "15x15"  <15>  "A 15x15 board is currently selected")
    (item "16x16"  <16>  "A 16x16 board is currently selected")
    (item "17x17"  <17>  "A 17x17 board is currently selected")
    (item "18x18"  <18>  "An 18x18 board is currently selected")
    (item "19x19"  <19>  "A 19x19 board is currently selected")
    }
)

(metadata
    (info {
        (description
"Quentin is a territorial connection game invented by Luis Bolanos Mures in 2012."
        )
        (rules
"Quentin is a drawless connection game for two players: Black and White. It's played on the intersections (points) of a square board, which is initially empty. The top and bottom edges of the board are colored black; the left and right edges are colored white.

Definitions:
A region is a set of one or more orthogonally contiguous empty points. A portion of a larger region is not a region.

A territory is a region such that every point in it is adjacent to at least two stones.

Play:
Black plays first, then turns alternate. On his turn, a player must place one stone of his color on an empty point.

Then, every territory on the board is filled with stones of the player who has the majority of stones orthogonally adjacent to it, after which the turn ends. Territories with the same number of Black and White stones adjacent to it are filled with stones of the opponent's color.

At the end of a turn, any two like-colored, diagonally adjacent stones must share at least one orthogonally adjacent, like-colored neighbor. Otherwise, the move is illegal and the player must choose another one.

If a player can't make a move on his turn, he must pass. Passing is otherwise not allowed. There will always be a move available to at least one of the players.

The game is won by the player who completes a chain of orthogonally contiguous stones of their color touching the two opposite board edges of their color. Draws are not possible.

Pie rule:
The pie rule is used in order to make the game fair. This means that White will have the option, on his first turn only, to change sides instead of making a regular move."
        )
        (version "1.3.3")
        (credit "Michael Amundsen")
        (author "Luis Bolanos Mures")
        (date "2012")
    })
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (board Colour InnerEdges (colour 178 151 100))
        (board Colour OuterEdges (colour Black))
        (board Colour Phase0 (colour 250 210 148))
        (board Colour Phase1 (colour 250 210 148))
        (board Style Board)
        (board StyleThickness OuterEdges 3.0)
        (region Colour Edge (union (sites Top Edge) (sites Bottom Edge)) regionSiteType:Edge (colour Black))
        (region Colour Edge (union (sites Left Edge) (sites Right Edge)) regionSiteType:Edge (colour White))
    })
)